plugins { id("org.gradlex.build-parameters") version "1.4.5" }

version = "0.12"

tasks.compileJava { options.release = 17 }

dependencies {
    implementation("com.diffplug.spotless:spotless-plugin-gradle:8.8.0") {
        // Exclude transitive dependencies of JGit as we do not need git functionality.
        // We can't exclude JGit itself as types are referenced in SpotlessTask.class.
        exclude("com.googlecode.javaewah", "JavaEWAH")
        exclude("commons-codec", "commons-codec")
        exclude("org.slf4j", "slf4j-api")
    }
    implementation("com.gradle.publish:plugin-publish-plugin:2.1.1")
    implementation("com.gradle:common-custom-user-data-gradle-plugin:2.7.0")
    implementation("com.gradle:develocity-gradle-plugin:4.5.0")
    implementation("com.gradleup.nmcp:nmcp:1.6.1")
    implementation("org.asciidoctor:asciidoctor5-jvm-core-plugin:5.0.0-alpha.1") {
        exclude(group = "io.ratpack")
        exclude(group = "io.netty")
        exclude(group = "org.codehaus.groovy")
        exclude(group = "io.github.http-builder-ng")
        exclude(group = "io.github.rburgst")
    }
    implementation("org.gradlex:jvm-dependency-conflict-resolution:2.5")
    implementation("org.gradlex:reproducible-builds:1.1")
}

dependencies.constraints {
    implementation("org.jetbrains:annotations:13.0!!") {
        because("This version is enforced by Gradle through the Kotlin plugin")
    }
}

buildParametersDefinition {
    pluginId("org.gradlex.internal.gradlex-build-parameters")
    bool("ci") {
        description = "Whether or not the build is running in a CI environment"
        fromEnvironment()
        defaultValue = false
    }
    group("signing") {
        // disable signing for local testing
        bool("disable") { defaultValue = false }
        // key and passphrase need default values because SigningExtension.useInMemoryPgpKeys does not accept providers
        description = "Details about artifact signing"
        string("key") {
            description = "The ID of the PGP key to use for signing artifacts"
            fromEnvironment()
            defaultValue = "UNSET"
        }
        string("passphrase") {
            description = "The passphrase for the PGP key specified by signing.key"
            fromEnvironment()
            defaultValue = "UNSET"
        }
    }
    group("pluginPortal") {
        // The publish-plugin reads these values directly from System.env. We model them here
        // for completeness and documentation purposes.
        description = "Credentials for publishing to the plugin portal"
        string("key") {
            description = "The Plugin portal key for publishing the plugin"
            fromEnvironment("GRADLE_PUBLISH_KEY")
        }
        string("secret") {
            description = "The Plugin portal secret for publishing the plugin"
            fromEnvironment("GRADLE_PUBLISH_SECRET")
        }
    }

    group("mavenCentral") {
        description = "Credentials for publishing to Maven Central"
        string("username") {
            description = "The Maven Central username for publishing"
            fromEnvironment()
        }
        string("password") {
            description = "The Maven Central password for publishing"
            fromEnvironment()
        }
    }
}

publishingConventions {
    pluginPortal("${project.group}.${project.name}") {
        implementationClass("org.gradlex.conventions.plugin.GradleXPluginConventionsPlugin")
        displayName("Conventions for building Gradle plugins")
        description("Conventions for building Gradle plugins used by all projects in the GradleX organisation.")
        tags("gradlex", "conventions", "publish", "plugins")
    }
    gitHub("https://github.com/gradlex-org/plugin-publish-conventions")
    developer {
        id = "britter"
        name = "Benedikt Ritter"
        email = "benedikt@gradlex.org"
    }
    developer {
        id = "jjohannes"
        name = "Jendrik Johannes"
        email = "jendrik@gradlex.org"
    }
    developer {
        id = "ljacomet"
        name = "Louis Jacomet"
        email = "louis@gradlex.org"
    }
}

// Do not publish a marker for 'build-parameters'
gradlePlugin.plugins.removeAll { it.name == "build-parameters" }
